if getos() == 'Windows' then    // Windows - COM*
    serialPort = 'COM7'
elseif getos() == 'Darwin' then // macOS - /dev/tty.usbmodem* ou /dev/tty.usbserial*
    error('Serial Communication Toolbox not supported on macOS')
else                            // Linux - /dev/ttyUSB* ou /dev/ttyACM*
    serialPort = '/dev/ttyACM0'
    usecanvas(%T)
end

ser = openserial(serialPort, '9600,n,8,1')

sleep(3, 's') // Bootloader Arduino

figure('position', [0, 0, 200, 200], ...
'menubar_visible', 'off', ...
'toolbar_visible', 'off', ...
'infobar_visible', 'off', ...
'dockable', 'off')

uicontrol('style', 'pushbutton', ...
'units', 'pixels', ...
'position', [60 20 80 40], ...
'string', 'Quit', ...
'callback', 'quitApp')

uicontrol('style', 'pushbutton', ...
'units', 'pixels', ...
'position', [60 80 80 40], ...
'string', 'LED Off', ...
'callback', 'turnLedOff')

uicontrol('style', 'pushbutton', ...
'units', 'pixels', ...
'position', [60 140 80 40], ...
'string', 'LED On', ...
'callback', 'turnLedOn')

function turnLedOn()
    writeserial(ser, '1')
endfunction

function turnLedOff()
    writeserial(ser, '0')
endfunction

function quitApp()
    closeserial(ser)
    close()
endfunction
